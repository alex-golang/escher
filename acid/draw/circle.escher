
Circle {
	circle CircleWrite
	circle: = :
	circle:View = :View
	circle:WriteTo = use.Output // use.Output should be planted by the materializer
}

CircleWrite { // : :View :WriteTo
	:WriteTo = form:

	form text.ForkForm
	form:Form = `<circle cx="{{.Gate.X}}" cy="{{.Gate.Y}}" r="{{.Gate.R}}" fill="rgba(0, 0, 0, 0.5)" />`
	form:Data = data:

	data Fork
	data:R = rad:R
	data:X = planar:X
	data:Y = planar:Y

	planar Fork
	planar: = cp:Planar

	cp cmplx.Planar
	cp:Complex = pos:

	pos view.QuickFocus
	pos:Name = "Position"
	pos:Root = j:0

	j Junction
	j: = :View
	j:1 = orient:Root
	j:2 = : // drawing is decoupled from continuation

	orient view.QuickFocus
	orient:Name = "Orientation"
	orient: = polar:Complex

	polar cmplx.Polar
	polar:Polar = rad:

	rad Fork
}

TestCircleWrite {
	circle CircleWrite
	circle:WriteTo = os.Stdout
	circle:View = {
		Position -1.200000-.400000i
		Orientation -1.200000+3.400000i
	}
	circle: = Show
}
