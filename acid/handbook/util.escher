
FigurePngSvgRight { // Caption, Image, Width, Right and default
	fig Figure
	fig:Caption = :Caption
	fig:Body = lr:
	fig: = :

	lr LeftRight
	lr:Right = :Right
	lr:Left = ps:

	ps PngSvg
	ps:Name = :Image
	ps:Width = :Width
}

LeftRight { // Left, Right and default valves
	t text.QuickForm
	t:Data = f:
	f Fork
	f:Left = :Left
	f:Right = :Right
	t:Form = `
	<table>
	<tr><td>{{.Gate.Left}}</td><td>{{.Gate.Right}}</td></tr>
	</table>
	`
	t: = :
}

FigurePngSvg { // Caption, Image, Width and default
	fig Figure
	fig:Caption = :Caption

	ps PngSvg	
	ps:Name = :Image
	ps:Width = :Width
	fig:Body = ps:

	fig: = :
}

Figure { // Body, Caption and default valves
	t text.QuickForm
	t:Form = `
	<p><center>
	<figure class="shadowless">
		{{.Gate.Body}}
		<div><em>{{.Gate.Caption}}</em></div>
	</figure>
	</center></p>
	`
	fork Fork
	fork:Body = :Body
	fork:Caption = :Caption
	t:Data = fork:

	t: = :
}

TestFigure {
	fig Figure
	fig:Body = "body"
	fig:Caption = "caption"
	fig: = Show
}

PngSvg { // Name and default valves
	t text.QuickForm
	t:Form = `<object data="img/{{.Gate.Name}}.svg" type="image/svg+xml" width="{{.Gate.Width}}">
		<img src="img/{{.Gate.Name}}.png" alt="" />
	</object>`
	t:Data = f:

	f Fork
	f:Name = :Name
	f:Width = :Width

	t: = :
}

TestPngSvg {
	ps PngSvg
	ps:Name = "NAME"
	ps:Width = "300px"
	ps: = Show
}
